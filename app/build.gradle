import com.android.tools.profgen.ArtProfileKt
import com.android.tools.profgen.ArtProfileSerializer
import com.android.tools.profgen.DexFile

plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
    id 'kotlin-kapt'
}

def taskRequests = getGradle().getStartParameter().getTaskRequests().toString()
def isPlay = !(taskRequests.contains("Foss") || taskRequests.contains("foss"))

if (isPlay) {
    apply from: "$project.rootDir/gradle/plugins.gradle"
}

apply from: "$project.rootDir/gradle/common.gradle"

android {
    namespace 'com.shifthackz.aisdv1.app'
    defaultConfig {
        applicationId "com.shifthackz.aisdv1.app"
        versionName "0.4.8"
        versionCode 155

        buildConfigField "String", "CLOUD_AI_URL", "\"https://sdai.moroz.cc\""
        buildConfigField "String", "HORDE_AI_URL", "\"https://stablehorde.net\""
        buildConfigField "String", "HORDE_AI_SIGN_UP_URL", "\"https://stablehorde.net/register\""
        buildConfigField "String", "UPDATE_API_URL", "\"https://sdai.moroz.cc\""
        buildConfigField "String", "DEMO_MODE_API_URL", "\"https://sdai.moroz.cc\""
        buildConfigField "String", "POLICY_URL", "\"https://sdai.moroz.cc/policy.html\""
        buildConfigField "String", "GITHUB_SOURCE_URL", "\"https://github.com/ShiftHackZ/Stable-Diffusion-Android\""
        buildConfigField "String", "SETUP_INSTRUCTIONS_URL", "\"https://github.com/AUTOMATIC1111/stable-diffusion-webui/wiki\""
    }

    def hasPropertiesFile = new File("app/keystore/signing.properties").exists()
    if (hasPropertiesFile) {
        Properties props = new Properties()
        props.load(new FileInputStream(file("keystore/signing.properties")))

        signingConfigs {
            release {
                storeFile file(props['keystore'])
                storePassword props['keystore.password']
                keyAlias props['keystore.alias']
                keyPassword props['keystore.password']
            }
        }
        buildTypes.release.signingConfig = signingConfigs.release
    }

    flavorDimensions "type"
    productFlavors {
        foss {
            dimension "type"
            applicationIdSuffix = ".foss"
            resValue "string", "app_name", "SDAI FOSS"
            buildConfigField "String", "BUILD_FLAVOR_TYPE", "\"FOSS\""
        }
        playstore {
            dimension "type"
            resValue "string", "app_name", "SDAI"
            buildConfigField "String", "BUILD_FLAVOR_TYPE", "\"GOOGLE_PLAY\""
        }
    }
}

dependencies {
    implementation project(":core:common")
    implementation project(":core:imageprocessing")
    implementation project(":core:validation")
    implementation project(":presentation")
    implementation project(":network")
    implementation project(":storage")
    implementation project(":domain")
    implementation project(":feature:ads")
    implementation project(":feature:analytics")
    implementation project(":feature:auth")
    implementation project(":data")
    implementation project(":demo")
    implementation di.koinCore
    implementation di.koinAndroid
    implementation di.koinCompose
    implementation reactive.rxkotlin
    implementation reactive.rxandroid
    implementation log.timber

    playstoreImplementation platform(proprietary.fbBom)
    playstoreImplementation proprietary.fbA
    playstoreImplementation proprietary.fbC
    playstoreImplementation proprietary.plA
}

kapt {
    correctErrorTypes true
}

// Workaround for "Bug: baseline.profm not deterministic"
//
// Reference     : https://f-droid.org/docs/Reproducible_Builds/#bug-baselineprofm-not-deterministic
// Fix snippet   : https://gist.github.com/obfusk/61046e09cee352ae6dd109911534b12e
// Issue tracker : https://issuetracker.google.com/issues/231837768
project.afterEvaluate {
    tasks.each { task ->
        if (task.name.startsWith("compile") && task.name.endsWith("ReleaseArtProfile")) {
            task.doLast {
                outputs.files.each { file ->
                    if (file.name.endsWith(".profm")) {
                        println("Sorting ${file} ...")
                        def version = ArtProfileSerializer.valueOf("METADATA_0_0_2")
                        def profile = ArtProfileKt.ArtProfile(file)
                        def keys = new ArrayList(profile.profileData.keySet())
                        def sortedData = new LinkedHashMap()
                        Collections.sort keys, new DexFile.Companion()
                        keys.each { key -> sortedData[key] = profile.profileData[key] }
                        new FileOutputStream(file).with {
                            write(version.magicBytes$profgen)
                            write(version.versionBytes$profgen)
                            version.write$profgen(it, sortedData, "")
                        }
                    }
                }
            }
        }
    }
}
